apply plugin: 'com.android.library'
apply plugin: 'com.github.johnrengelman.shadow'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    def labClientSecret = "" // will be blank unless specified by developer

    if (project.hasProperty("labSecret")) {
        labClientSecret = labSecret
    }

    def uploadTestResultsToKusto = false

    if (project.hasProperty("sendTestResultsToKusto")) {
        uploadTestResultsToKusto = getBoolean(sendTestResultsToKusto);
    }

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "LAB_CLIENT_SECRET", "\"$labClientSecret\"")
        buildConfigField("boolean", "UPLOAD_TEST_RESULTS_TO_KUSTO", "$uploadTestResultsToKusto")
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }

    buildTypes {
        debug {
            testCoverageEnabled false
            debuggable true
        }
        release {
            testCoverageEnabled false
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            exclude 'com/microsoft/identity/internal/testutils/lab/'
        }
    }

}

configurations {
    repackage // configuration to repackage a jar
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

/* a task to create the relocated libs */
task repackageJar(type: ShadowJar) {
    def originalPkg = 'org.apache.http' // lib to relocate
    def shadowPkg = 'cz.msebera.android.httpclient' // the lib that will be substituted
    relocate originalPkg, shadowPkg

    // excluding these from package to allow repackaging
    exclude 'META-INF/*.MF'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.SF'

    dependencies {
        // we are already pulling these from other dependencies
        exclude(dependency("com.google.guava:guava:.*"))
        exclude(dependency("org.ow2.asm:asm:.*"))
        exclude(dependency("com.google.code.gson:gson:.*"))
    }

    configurations = [project.configurations.repackage] // our configuration from above
}

tasks.whenTaskAdded { task ->
    task.dependsOn "repackageJar"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':common')
    implementation project(':keyvault')
    implementation project(':labapi')
    implementation "org.mockito:mockito-android:$rootProject.ext.mockitoAndroidVersion"
    implementation "androidx.test:core:$rootProject.ext.androidxTestCoreVersion"
    implementation "com.google.code.gson:gson:$rootProject.ext.gsonVersion"
    implementation "com.nimbusds:nimbus-jose-jwt:$rootProject.ext.nimbusVersion"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation "junit:junit:$rootProject.ext.junitVersion"
    implementation "com.opencsv:opencsv:5.2"
    compileOnly "org.projectlombok:lombok:$rootProject.ext.lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$rootProject.ext.lombokVersion"
    testImplementation "org.robolectric:robolectric:$rootProject.ext.robolectricVersion"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    repackage 'com.microsoft.azure.kusto:kusto-ingest:2.1.0'
    implementation tasks.repackageJar.outputs.files
}

static boolean getBoolean(final String s) {
    if (s == null || s == "") {
        return false;
    } else if (s.equalsIgnoreCase("true")) {
        return true;
    } else {
        return false;
    }
}
