plugins {
    id 'java-library'
    id 'com.microsoft.identity.buildsystem' version '0.1.1'

    // the below build plugin allows to generate BuildConfig files in a JAVA gradle project.
    // BuildConfig files are generated by default in an Android project, however, a non-Android java
    // module doesn't really have a concept of BuildConfig. This plugin allows us to create similar
    // looking BuildConfig constants in a java project.
    id 'com.github.gmazzo.buildconfig' version '3.0.0'
}

def labClientSecret = "" // will be blank unless specified by developer

if (project.hasProperty("labSecret")) {
    labClientSecret = labSecret
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

buildSystem {
    desugar = false
}

buildConfig {
    // forces the package. Defaults to '${project.group}'
    packageName("com.microsoft.identity.labapi.utilities")
    useJavaOutput() // forces the outputType to 'java'
}


sourceSets {
    main {
        java.srcDirs = ['src/main']
    }
    test {
        java.srcDirs = ['src/test', 'build/generated/source/buildConfig/test']
        buildConfigField("String", "LAB_CLIENT_SECRET", "\"$labClientSecret\"")
    }
}

dependencies {
    // keyvault per product flavor
    if (findProject(":keyvault") != null) {
        //Use project reference if available
        implementation(project(":keyvault")) {
            transitive = true
        }
    } else {
        //Always use the latest release
        implementation(group: 'com.microsoft.identity', name: 'keyvault', version: '0.0.+')
    }

    // labapi per product flavor
    if (findProject(":labapi") != null) {
        //Use project reference if available
        implementation(project(":labapi")) {
            transitive = true
        }
    } else {
        //Always use the latest release
        implementation(group: 'com.microsoft.identity', name: 'labapi', version: '0.0.+')
    }

    implementation "com.nimbusds:nimbus-jose-jwt:$rootProject.ext.nimbusVersion"
    implementation "com.microsoft.azure:msal4j:$rootProject.ext.msal4jVersion"
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    compileOnly "org.projectlombok:lombok:$rootProject.ext.lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$rootProject.ext.lombokVersion"
}

afterEvaluate {
    // these tasks are generated by the buildconfig plugin..for more details, read comment about it
    // above in the plugins block.
    assemble.dependsOn generateBuildConfig, generateTestBuildConfig
}
